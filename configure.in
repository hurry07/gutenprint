dnl Process this file with autoconf to produce a configure script.
AC_INIT(print.c)
AM_INIT_AUTOMAKE(print,4.1.2)
AM_CONFIG_HEADER(config.h)

dnl Compiler options...
CFLAGS="${CFLAGS:=}"
OPTIM=""

AC_MSG_CHECKING([whether to enable debugging])
AC_ARG_ENABLE(debug,
[  --enable-debug          turn on debugging [default=no]],[
if eval "test x$enable_debug = xyes"; then
  AC_MSG_RESULT([yes])
  OPTIM="-g "
else
  AC_MSG_RESULT([no])
fi],[AC_MSG_RESULT([no])])

AC_MSG_CHECKING([if GIMP checks are enabled])
AC_ARG_ENABLE(gimptest,
[  --enable-gimptest       check for the gimp [default=yes]],[
if eval "test x$enable_gimptest = xno"; then
  AC_MSG_RESULT([no])
  DISABLE_GIMP="disabled"
else
  AC_MSG_RESULT([yes])
fi],[AC_MSG_RESULT([yes])])

AC_MSG_CHECKING([if user install is enabled])
AC_ARG_ENABLE(user-install,
[  --enable-user-install   install GIMP plugin in home directory [default=no]],[
if eval "test x$enableval = xyes"; then
  AC_MSG_RESULT([yes])
  PLUG_IN_PATH="bin"
else
  AC_MSG_RESULT([no])
  PLUG_IN_PATH="admin-bin"
fi],[
AC_MSG_RESULT([no])
PLUG_IN_PATH="admin-bin"
])
 	
dnl Checks for programs.
AC_ISC_POSIX
AC_PROG_AWK
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_YACC
AM_PROG_LEX
AC_PATH_PROG(BISON_PROG, bison)
AC_PATH_PROG(YACC_PROG, yacc)

RELDATE="\"`date '+%d %b %Y'`\""
RELDATE_DEF="-DRELEASE_DATE=\"\\\"`date '+%d %b %Y'`\\\"\""

AM_MAINTAINER_MODE

if test "$USE_MAINTAINER_MODE" = "yes" ; then
  MAINTAINER_PROGRAMS='escp2-weavetest printer_options'
else
  MAINTAINER_PROGRAMS=''
fi

if test -n "$GCC"; then
  # Only GCC supports -Wall and other GCC options...
  if test "$USE_MAINTAINER_MODE" = "yes" ; then
    MAINTAINER_CFLAGS='-Wall -Wcast-align -Wstrict-prototypes -DGIMP_PRINT_MAINT'
  else
    MAINTAINER_CFLAGS='-Wall -Wcast-align -Wstrict-prototypes'
  fi

  if test -z "$CFLAGS" ; then
    if test "x$OPTIM" = "x"; then
      CFLAGS="$CFLAGS -O2"
    else
      CFLAGS="$CFLAGS -g"
    fi
  fi
else
  if test "$USE_MAINTAINER_MODE" = "yes" ; then
    MAINTAINER_CFLAGS='-DGIMP_PRINT_MAINT'
  else
    MAINTAINER_CFLAGS=''
  fi

  if test -z "$CFLAGS" ; then
    if test "x$OPTIM" = "x"; then
      case `uname` in
        IRIX*)
	  CFLAGS="$CFLAGS -O3"
	  ;;
        *)
          CFLAGS="$CFLAGS -O"
          ;;
      esac
    else
      CFLAGS="$CFLAGS -g"
    fi
  fi
fi

if test "$DISABLE_GIMP" = "disabled" ; then
  echo "Skipping test for the Gimp"
else
  AM_PATH_GIMP(1.0.0,
        [SAVE_GTK_LIBS="$GIMP_LIBS"
        SAVE_GTK_CFLAGS="$GIMP_CFLAGS"],
        AC_MSG_ERROR(Cannot find GIMP libs: Please run ldconfig as root, and make sure gimptool is on your PATH.))
fi

dnl Checks for libraries.
AC_CHECK_LIB(readline, readline, HAVE_LIBREADLINE=true, HAVE_LIBREADLINE=false)
if test x${HAVE_LIBREADLINE} = xtrue ; then
  AC_DEFINE(HAVE_LIBREADLINE,1)
else
  AC_DEFINE(HAVE_LIBREADLINE,0)
fi

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(unistd.h)
AC_CHECK_HEADER(readline/readline.h, HAVE_READLINE_READLINE_H=true, HAVE_READLINE_READLINE_H=false)
if test x${HAVE_READLINE_READLINE_H} = xtrue ; then
  AC_DEFINE(HAVE_READLINE_READLINE_H)
  if test x${HAVE_LIBREADLINE} = xtrue ; then
    LIBREADLINE_DEPS="-lreadline"
  fi
fi



dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_OFF_T
AC_TYPE_SIZE_T

dnl Checks for library functions.
AC_FUNC_MEMCMP
AC_TYPE_SIGNAL
AC_CHECK_FUNC(random, AC_DEFINE(HAVE_RANDOM))

AC_SUBST(GIMP_LIBS)
AC_SUBST(PLUG_IN_PATH)
AC_SUBST(LIBREADLINE_DEPS)
AC_SUBST(RELDATE_DEF)
AC_SUBST(MAINTAINER_PROGRAMS)
AC_SUBST(MAINTAINER_CFLAGS)
AC_SUBST(RELDATE)

AC_OUTPUT([
Makefile
escputil.1
],[
echo "Finished configuring."
echo "Type 'make' to build the package"
echo "then 'make install' to install it."
])
